#pragma kernel Clear
#pragma kernel SSPR
#pragma kernel FillHole

#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Input.hlsl"

SamplerState linearClampSampler;
// input
Texture2D<float4> _CameraDepthTexture;
Texture2D<float4> _CameraOpaqueTexture;
float4 _SSPRParam1;
float4 _SSPRParam2;
// define
#define _TextureSize float4(1.0 / _SSPRParam1.x, 1.0 / _SSPRParam1.y, _SSPRParam1.xy)
#define _WaterHeight _SSPRParam1.z
#define _SampleOffset _SSPRParam1.w
#define _StretchIntensity _SSPRParam2.x
#define _StretchThreshold _SSPRParam2.y
#define _CameraDirY _SSPRParam2.z
#define _FadeAdjust _SSPRParam2.w
#define MAXUINT 0xFFFFFFFF
#define FillHoleDst 10
// output
#if SHADER_API_METAL
RWBuffer<uint> _SSPRBuffer;
uint GetIndex(uint2 id) { return id.y * _TextureSize.z + id.x; }
#define INDEX(id) GetIndex(id)
#else
RWTexture2D<uint> _SSPRBuffer;
#define INDEX(id) id
#endif

RWTexture2D<float4> _SSPRTextureResult;

[numthreads(8, 8, 1)]
void Clear (uint3 id : SV_DispatchThreadID)
{
#if UNITY_UV_STARTS_AT_TOP
    _SSPRBuffer[INDEX(id.xy)] = MAXUINT;
#else
    _SSPRBuffer[INDEX(id.xy)] = 0;
#endif
    _SSPRTextureResult[id.xy] = 0;
}

// 获得世界坐标
float3 GetWorldPositionFromDepth(float depth, float2 uv)
{
#if UNITY_UV_STARTS_AT_TOP
    uv.y = 1 - uv.y;
#endif
    float4 positionCS = float4(uv * 2.0 - 1.0, depth, 1);
    float4 positionWS = mul(UNITY_MATRIX_I_VP, positionCS);
    positionWS.xyz /= positionWS.w;
    return positionWS.xyz;
}
// 编码
uint Encode(uint2 id)
{
    return id.y << 16 | id.x;
}
// 解码
uint2 Decode(uint encode)
{
    return uint2(encode & 0xFFFF, encode >> 16);
}

[numthreads(8, 8, 1)]
void SSPR (uint3 id : SV_DispatchThreadID)
{
    half2 screenUV = (id.xy + 0.5) * _TextureSize.xy;
    half sampleDepth = _CameraDepthTexture[id.xy * _SampleOffset].r;
    if (Linear01Depth(sampleDepth, _ZBufferParams) > 0.95) return;
    
    float3 positionWS = GetWorldPositionFromDepth(sampleDepth, screenUV);
    if (positionWS.y > _WaterHeight)
    {
        float4 positionRWS = float4(positionWS.x, 2 * _WaterHeight - positionWS.y, positionWS.z, 1);
        float4 positionRCS = mul(UNITY_MATRIX_VP, positionRWS);
        float4 positionSS = ComputeScreenPos(positionRCS);
        float2 reflectUV = positionSS.xy / positionSS.w;
        // 伸展uv
        reflectUV.x = reflectUV.x * 2.0 - 1.0;
	    float HeightStretch = positionWS.y - _WaterHeight;
        float AngleStretch = max(0, _CameraDirY);
        float ScreenStretch = saturate(abs(reflectUV.x) - _StretchThreshold);
        reflectUV.x *= 1 + HeightStretch * AngleStretch * ScreenStretch * _StretchIntensity;
	    reflectUV.x = reflectUV.x * 0.5 + 0.5;
        // limit min value
        uint2 reflectID = uint2(reflectUV * _TextureSize.zw);
        uint encodeID = Encode(id.xy);
#if UNITY_UV_STARTS_AT_TOP
        InterlockedMin(_SSPRBuffer[INDEX(reflectID)], encodeID);
#else
        InterlockedMax(_SSPRBuffer[INDEX(reflectID)], encodeID);
#endif
    }
}
// 获得周围的坐标
uint GetNearbyBuffer(uint2 id, int2 offset)
{
    uint2 nearbyID = id + offset;
    nearbyID.x = clamp(nearbyID.x, 0, _TextureSize.z);
    nearbyID.y = clamp(nearbyID.y, 0, _TextureSize.w);
    return _SSPRBuffer[INDEX(nearbyID)];
}
[numthreads(8, 8, 1)]
void FillHole (uint3 id : SV_DispatchThreadID)
{
    uint encodeCurrentID = _SSPRBuffer[INDEX(id.xy)];
    uint upperEncodeID = GetNearbyBuffer(id.xy, int2(0, 1));
    uint underEncodeID = GetNearbyBuffer(id.xy, int2(0, -1));
    uint leftEncodeID = GetNearbyBuffer(id.xy, int2(-1, 0));
    uint rightEncodeID = GetNearbyBuffer(id.xy, int2(1, 0));
#if UNITY_UV_STARTS_AT_TOP
    uint targetEncodeID = min(min(upperEncodeID, underEncodeID), min(leftEncodeID, rightEncodeID));
#else
    uint targetEncodeID = max(max(upperEncodeID, underEncodeID), max(leftEncodeID, rightEncodeID));
#endif

    uint2 decodeCurrentID = Decode(encodeCurrentID);
    uint2 decodeTargetID = Decode(targetEncodeID);

    bool isCanFillHole = true;
#if UNITY_UV_STARTS_AT_TOP
    if (encodeCurrentID != MAXUINT)
#else
    if (encodeCurrentID != 0)
#endif
    {
        uint2 offset = decodeTargetID - decodeCurrentID;
        isCanFillHole = dot(offset, offset) < FillHoleDst * FillHoleDst;
    }
    if (isCanFillHole)
    {
        decodeCurrentID = decodeTargetID;
        encodeCurrentID = targetEncodeID;
    }

    half2 uv = (decodeCurrentID + 0.5) * _TextureSize.xy;
    half4 reflectionColor = _CameraOpaqueTexture.SampleLevel(linearClampSampler, uv, 0);
    half alpha = smoothstep(1, _FadeAdjust, decodeCurrentID.y * _TextureSize.y);
#if UNITY_UV_STARTS_AT_TOP
    _SSPRTextureResult[id.xy] = encodeCurrentID == MAXUINT ? 0 : half4(reflectionColor.rgb, alpha);
#else
    _SSPRTextureResult[id.xy] = encodeCurrentID == 0 ? 0 : half4(reflectionColor.rgb, alpha);
#endif
}